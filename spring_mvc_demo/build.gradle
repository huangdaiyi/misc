apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'findbugs'

version = '0.0.1'
sourceCompatibility = 1.7
targetCompatibility = 1.7
[compileJava, javadoc, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
	maven {
		url 'http://10.16.131.148:8081/nexus/service/local/repositories/public/content/'
	}
	mavenCentral()
}
sourceSets {
	main {
		java {
			srcDirs = [ 'src/main/java' ]
		}
		resources {
			srcDirs = [ 'src/main/java', 'src/main/resources' ]
		}
	}
	test {
		java {
			srcDirs = [ 'src/test/java' ]
		}
		resources {
			srcDirs = [ 'src/test/java', 'src/main/resources' ]
		}
	}
}

// copy resources into classpath for testing
task copyTestResources(type: Copy) {
	from sourceSets.test.resources
	into sourceSets.test.output.classesDir
}
processTestResources.dependsOn copyTestResources

dependencies {
	compile (
		// spring
		'org.springframework:spring-webmvc:4.1.6.RELEASE',
		// json
		'com.fasterxml.jackson.core:jackson-databind:2.5.4',
		// database
		'com.mchange:c3p0:0.9.5.1',
		'org.springframework:spring-jdbc:4.1.6.RELEASE',
		'mysql:mysql-connector-java:5.1.36',
		// aws
		'com.amazonaws:aws-java-sdk-dynamodb:1.9.40',
		// sqs
		'com.amazonaws:aws-java-sdk-sqs:1.9.40',
		// sms
		'com.twilio.sdk:twilio-java-sdk:3.7.2',
		// logger
		'org.apache.logging.log4j:log4j-core:2.3',
		'org.apache.logging.log4j:log4j-jcl:2.3',
		// aop
		'org.aspectj:aspectjrt:1.8.6',
		'org.aspectj:aspectjweaver:1.8.6',
		// xmpp
		'org.igniterealtime.smack:smack-tcp:4.0.7',
		// guava
		'com.google.guava:guava:18.0',
		// slf4j
		'org.slf4j:slf4j-jdk14:1.7.12',
		// others
		'commons-io:commons-io:2.4',
		// httpclient
		'org.apache.httpcomponents:httpclient:4.5'
	)
	providedCompile (
		'javax.servlet:javax.servlet-api:3.1.0'
	)
	testCompile (
		'junit:junit:4.12',
		'org.springframework:spring-test:4.1.6.RELEASE',
		// mock
		'org.mockito:mockito-all:1.10.19',
		'org.kubek2k:springockito-annotations:1.0.9',
		'org.kubek2k:springockito:1.0.9',
		// findbugs
		'com.google.code.findbugs:findbugs:3.0.1',
		// pmd
		'net.sourceforge.pmd:pmd:5.3.2'
	)
}

compileJava {
	// enable compilation in a separate daemon process
	options.fork = true
	// enable incremental compilation
	options.incremental = true
}

eclipse {
	wtp {
		facet {
			facet name: 'jst.web', version: '3.0'
			facet name: 'java', version: '1.7'
			facet name: 'wst.jsdt.web', version: '1.0'
		}
	}
}

findbugs {
	toolVersion = "2.0.1"
	sourceSets = [sourceSets.main]
	reportsDir = file("$project.buildDir/findbugsReports")
	effort = "max"
	reportLevel = "high"
	visitors = ["FindSqlInjection", "SwitchFallthrough"]
	omitVisitors = ["FindNonShortCircuit"]
}

war {
	from ('tools') {
		into 'WEB-INF/tools'
	}
	from ('db_init_script') {
		into 'WEB-INF/db_init_script'
	}
	from ('db_migration_script') {
		into 'WEB-INF/db_migration_script'
	}
}
